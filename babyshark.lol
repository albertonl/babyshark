HAI 1.2
    CAN HAS STDIO?
    HOW IZ I FAMILY YR INDEX
        OBTW
            I'll fix this mess whenever LOLCODE adds support for arrays.
        TLDR
        INDEX, WTF?
            OMG 0
                VISIBLE "Baby Shark"!
                GTFO
            OMG 1
                VISIBLE "Mommy Shark"!
                GTFO
            OMG 2
                VISIBLE "Daddy Shark"!
                GTFO
            OMG 3
                VISIBLE "Grandma Shark"!
                GTFO
            OMG 4
                VISIBLE "Grandpa Shark"!
                GTFO
            OMG 5
                VISIBLE "Let's go hunt"!
                GTFO
            OMG 6
                VISIBLE "Run away"!
                GTFO
            OMG 7
                VISIBLE "Safe at last"!
                GTFO
            OMG 8
                VISIBLE "It's the end"!
                GTFO
        OIC
    IF U SAY SO

    I HAS A SECTION ITZ 0
    I HAS A FAMILYSIZE ITZ 9
    I HAS A LINECOUNT

    IM IN YR SHARKIN UPPIN YR SECTION TIL BOTH SAEM SECTION AN FAMILYSIZE
        LINECOUNT R 0
        IM IN YR PRINTIN UPPIN YR LINECOUNT TIL BOTH SAEM LINECOUNT AN 3
            I IZ FAMILY YR SECTION MKAY
            VISIBLE ", doo doo doo doo doo doo"
        IM OUTTA YR PRINTIN

        I IZ FAMILY YR SECTION MKAY
        VISIBLE "!"
    IM OUTTA YR SHARKIN
KTHXBYE